{
	function log(message) {
		console.log(message);
	}
}

start
	= _ "#types;" _ x:type_list "#relationships;" _ y:relationship_list _
	"#actions;" _ z:action_list _
	{
		var primitives = [];
		for (var i = 0; i < x.length; i++) {
			if (x[i][0].indexOf("*") != -1) {
				primitives.push(x[i]);
			}
		}
		return {
			type_list: x,
			relationship_list: y,
			action_list: z,
			primitive_list: primitives
		}
	}
	
type_list
	= x:type_def y:type_list _
	{
		return [x].concat(y);
	}
	/ _
	{
		return [];
	}
	
type_def
	= x:type_token _ ";" _
	{
		return [x, []];
	}
	/ x:type_token _ "->" _ y:type_token ";" _
	{
		return [x, [y]];
	}
	
type_token
	= x:([a-zA-Z]+)y:([*]?)
	{
		if (y == null) {
			return x.join("");
		}
		else {
			return x.join("") + "*";
		}
	}
	
relationship_list
	= x:relationship_def y:relationship_list _
	{
		return [x].concat(y);
	}
	/ _
	{
		return [];
	}
		
relationship_def
	= x:predicate_token _ "(" _ y:relationship_parameter_list? _ ")" _ ";" _
	{
		if (y == null) y = [];
		var res = {
			predicate: x,
			parameters: y
		}
		return res;
	}
	
predicate_token
	= x:[a-zA-Z]+
	{
		return x.join("");
	}
	
relationship_parameter_list
	= x:type_token __ y:relationship_parameter_list _
	{
		return [x].concat(y);
	}
	/ x:type_token	
	{
		return [x];
	}
	
action_list
	= "name:" _ a:action_name_token _ ";" _ "parameters:" _ b:action_parameter_list? _ ";" _ "preconditions:" _ c:action_condition_list? _ ";" _   "effects:" _ d:action_condition_list? _ ";" _ e:action_list
	{
		if (b == null) {
			b = [];
		}
		if (c == null) {
			c = [];
		}
		if (d == null) {
			d = [];
		}
		return [{
			name: a,
			parameters: b,
			preconditions: c,
			effects: d
		}].concat(e)
	}
	/ _
	{
		return [];
	}
	
action_name_token
	= x:[a-zA-Z ]+
	{
		return x.join("");
	}

action_parameter_list
	= x:type_token __ y:action_parameter_list
	{
		return [x].concat(y);
	}
	/ x:wildcard_token __ y:action_parameter_list
	{
		return [x].concat(y);
	}
	/ x:type_token
	{
		return [x];
	}
	/ x:wildcard_token
	{
		return [x];
	}
	
wildcard_token
	= x:"*"
	{
		return "*";
	}

action_condition_list
	= a:("!"?) b:predicate_token _ "(" c:assertion_parameter_list? _ ")" __ d:action_condition_list
	{
		if (a == null) {
			a = true;
		}
		else {
			a = false;
		}
		if (c == null) {
			c = [];
		}
		return [{
			truth: a,
			predicate: b,
			parameters: c
		}].concat(d);
	}
	/ a:("!"?) b:predicate_token _ "(" c:assertion_parameter_list? _ ")" _
	{
		if (a == null) {
			a = true;
		}
		else {
			a = false;
		}
		if (c == null) {
			c = [];
		}
		return [{
			truth: a,
			predicate: b,
			parameters: c
		}];
	}

assertion_parameter_list
	= x:type_token __ y:assertion_parameter_list
	{
		return [x].concat(y);
	}
	/ x:assertion_variable_token __ y:assertion_parameter_list
	{
		return [x].concat(y);
	}
	/ x:type_token
	{
		return [x];
	}
	/ x:assertion_variable_token
	{
		return [x];
	}

assertion_variable_token
	= x:[0-9]+
	{
		return x.join("");
	}
	
_
	= [ \t\r\n]*

__
	= [ \t\r\n]+